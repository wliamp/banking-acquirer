import static java.lang.System.getenv
import static org.gradle.api.JavaVersion.VERSION_17
import static org.gradle.jvm.toolchain.JavaLanguageVersion.of

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.7'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco'
}

java {
    toolchain {
        languageVersion = of(25)
    }
    sourceCompatibility = VERSION_17
    targetCompatibility = VERSION_17
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    maven {
        url = uri("https://maven.pkg.github.com/hamsaqua/starter")
        credentials {
            username = getenv("GITHUB_ACTOR") ?: findProperty("github.actor")
            password = getenv("GITHUB_TOKEN") ?: findProperty("bank.aqr.core.token")
        }
    }
}

dependencies {
    implementation libs.starter
    implementation 'org.springframework:spring-context'
    implementation 'io.projectreactor:reactor-core'
    implementation 'org.springframework.data:spring-data-r2dbc'
    implementation 'io.r2dbc:r2dbc-spi'
    runtimeOnly 'io.r2dbc:r2dbc-postgres'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'io.projectreactor:reactor-test'
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
    finalizedBy(tasks.jacocoTestReport)
}


tasks.named('jacocoTestReport', JacocoReport) {
    dependsOn tasks.test
    reports {
        xml.required.set(true)
        html.required.set(true)
    }
}
